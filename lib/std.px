
func ord(Char c) Int extern
func chr(Int c) Char extern

func str([Char] a) String extern

func write(String s) extern
func writeLine(String s) extern

func read() String extern
func readLine() String extern
func readInt() Int extern
func readChar() Char extern


func min(Int a, Int b) Int def
    return a < b ? a : b

func max(Int a, Int b) Int def
    return b < a ? a : b


func Int_pow(Int b, Int e) Int def
    if e < 0 do
        return 0
    r = 1
    while e > 0 do
        if e % 2 > 0 do
            r *= b
        b *= b
        e /= 2
    return r

func Float_pow(Float b, Float e) Float extern


func CharArray_join([Char] array, String sep: "") String def
    m = sep.length
    n = array.length
    r = [' '] * ((m+1) * (n-1) + 1)
    p = 0
    for i in 0...n do
        r[p] = array[i]
        p += 1
        if i < n-1 do
            for c in sep do
                r[p] = c
                p += 1
    return str(r)

func StringArray_join([String] array, String sep: "") String def
    m = sep.length
    n = array.length
    l = 0
    for s in array do
        l += s.length
    r = [' '] * (l + m*(n-1))
    p = 0
    for i in 0...n do
        for c in array[i] do
            r[p] = c
            p += 1
        if i < n-1 do
            for c in sep do
                r[p] = c
                p += 1
    return str(r)


func Int_toString(Int n) String extern

func Float_toString(Float f) String extern

func Bool_toString(Bool b) String def
    return b ? "true" : "false"

func Char_toString(Char c) String def
    return str([c])

func String_toString(String s) String def
    return s


func String_toInt(String s) Int extern

func String_toFloat(String s) Float extern

func String_compare(String a, String b) Int def
    for i in 0...min(a.length, b.length) do
        if a[i] < b[i] do
            return -1
        elif b[i] < a[i] do
            return 1
    if a.length < b.length do
        return -1
    elif b.length < a.length do
        return 1
    return 0


func Int_toFloat(Int n) Float extern

func Float_toInt(Float f) Int extern
