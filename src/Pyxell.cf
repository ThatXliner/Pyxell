
-- layout --

layout             "do", "def" ;
layout stop        "elif", "else" ;
layout toplevel    ;


-- program --

Program.           Program ::= [Stmt] ;
separator          Stmt        ";" ;


-- statements --

SProc.             Stmt   ::= "func" Ident "(" [Arg] ")" "def" Block ;
SFunc.             Stmt   ::= "func" Ident "(" [Arg] ")" Type "def" Block ;
SProcExtern.       Stmt   ::= "func" Ident "(" [Arg] ")" "extern" ;
SFuncExtern.       Stmt   ::= "func" Ident "(" [Arg] ")" Type "extern" ;
ANoDef.            Arg    ::= Type Ident ;
--ADef.              Arg    ::= Type Ident "=" Expr ;
separator          Arg        "," ;
SRetVoid.          Stmt   ::= "return" ;
SRetExpr.          Stmt   ::= "return" Expr ;

SBlock.            Block   ::= "{" [Stmt] "}" ;

SSkip.             Stmt   ::= "skip" ;
SPrint.            Stmt   ::= "print" Expr ;

SAssg.             Stmt   ::= [Expr];
separator nonempty Expr       "=" ;
SAssgMul.          Stmt   ::= Expr "*=" Expr;
SAssgDiv.          Stmt   ::= Expr "/=" Expr;
SAssgMod.          Stmt   ::= Expr "%=" Expr;
SAssgAdd.          Stmt   ::= Expr "+=" Expr;
SAssgSub.          Stmt   ::= Expr "-=" Expr;

SIf.               Stmt   ::= "if" [Branch] Else ;
BElIf.             Branch ::= Expr "do" Block ;
separator          Branch     "elif" ;
EElse.             Else   ::= "else" "do" Block ;
EEmpty.            Else   ::= ;

SWhile.            Stmt   ::= "while" Expr "do" Block ;
SUntil.            Stmt   ::= "until" Expr "do" Block ;
SFor.              Stmt   ::= "for" Expr "in" Expr "do" Block ;
SContinue.         Stmt   ::= "continue" ;
SBreak.            Stmt   ::= "break" ;


-- expressions --

EInt.              Expr9  ::= Integer ;
ETrue.             Expr9  ::= "true" ;
EFalse.            Expr9  ::= "false" ;
EChar.             Expr9  ::= Char ;
EString.           Expr9  ::= String ;
EArray.            Expr9  ::= "[" [Expr2] "]" ;
separator          Expr2      "," ;

EVar.              Expr9  ::= Ident ;
EElem.             Expr9  ::= Expr9 "." Integer ;
EIndex.            Expr9  ::= Expr9 "[" Expr "]" ;
EAttr.             Expr9  ::= Expr9 "." Ident ;
ECall.             Expr9  ::= Expr9 "(" [Expr2] ")";

EMul.              Expr8  ::= Expr8 "*" Expr9 ;
EDiv.              Expr8  ::= Expr8 "/" Expr9 ;
EMod.              Expr8  ::= Expr8 "%" Expr9 ;
EAdd.              Expr7  ::= Expr7 "+" Expr8 ;
ESub.              Expr7  ::= Expr7 "-" Expr8 ;
ENeg.              Expr7  ::= "-" Expr8;

ERangeIncl.        Expr6  ::= Expr7 ".." Expr7 ;
ERangeExcl.        Expr6  ::= Expr7 "..." Expr7 ;
ERangeInclStep.    Expr6  ::= Expr7 ".." Expr7 ".." Expr7 ;
ERangeExclStep.    Expr6  ::= Expr7 "..." Expr7 ".." Expr7 ;

ECmp.              Expr5  ::= Cmp ;
Cmp1.              Cmp    ::= Expr6 CmpOp Expr6 ;
Cmp2.              Cmp    ::= Expr6 CmpOp Cmp ;
CmpEQ.             CmpOp  ::= "==" ;
CmpNE.             CmpOp  ::= "<>" ;
CmpLT.             CmpOp  ::= "<" ;
CmpLE.             CmpOp  ::= "<=" ;
CmpGT.             CmpOp  ::= ">" ;
CmpGE.             CmpOp  ::= ">=" ;

ENot.              Expr5  ::= "not" Expr5 ;
EAnd.              Expr4  ::= Expr5 "and" Expr4 ;
EOr.               Expr3  ::= Expr4 "or" Expr3 ;

ETuple.            Expr1  ::= [Expr3] ;
separator nonempty Expr3      "," ;

coercions          Expr       9 ;


-- types --

internal TPtr.     Type4  ::= Type4 ;
internal TDeref.   Type4  ::= Type4 ;
internal TLabel.   Type4  ::= "Label" ;
TVoid.             Type4  ::= "Void" ;
TInt.              Type4  ::= "Int" ;
TBool.             Type4  ::= "Bool" ;
TChar.             Type4  ::= "Char" ;
TObject.           Type4  ::= "Object" ;
TString.           Type4  ::= "String" ;
TArray.            Type4  ::= "[" Type "]" ;
TTuple.            Type2  ::= [Type3] ;
separator nonempty Type3      "*" ;
separator nonempty Type2      "," ;
TFunc.             Type1  ::= [Type2] "->" Type2 ;

coercions          Type       4 ;


-- comments --

comment            "#" ;
comment            "/*" "*/" ;