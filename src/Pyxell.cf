
-- layout --

layout             "do" ;
layout stop        "elif", "else" ;
layout toplevel    ;


-- program --


Program.           Program ::= [Stmt] ;
separator          Stmt        ";" ;


-- statements --

--SFunc.             Stmt   ::= "func" Ident "(" [Arg] ")" Type ":" Block ;
--ANoDef.            Arg    ::= Type Ident ;
--ADef.              Arg    ::= Type Ident "=" Expr ;
--separator          Arg        ";" ;

SBlock.            Block   ::= "{" [Stmt] "}" ;

SSkip.             Stmt   ::= "skip" ;

SPrint.            Stmt   ::= "print" Expr ;

SAssg.             Stmt   ::= [Expr];
separator nonempty Expr       "=" ;
SAssgMul.          Stmt   ::= Expr "*=" Expr;
SAssgDiv.          Stmt   ::= Expr "/=" Expr;
SAssgMod.          Stmt   ::= Expr "%=" Expr;
SAssgAdd.          Stmt   ::= Expr "+=" Expr;
SAssgSub.          Stmt   ::= Expr "-=" Expr;

SIf.               Stmt   ::= "if" [Branch] Else ;
BElIf.             Branch ::= Expr "do" Block ;
separator          Branch     "elif" ;
EElse.             Else   ::= "else" "do" Block ;
EEmpty.            Else   ::= ;

SWhile.            Stmt   ::= "while" Expr "do" Block ;


-- expressions --

EInt.              Expr8  ::= Integer ;
ETrue.             Expr8  ::= "true" ;
EFalse.            Expr8  ::= "false" ;
EString.           Expr8  ::= String ;
EArray.            Expr8  ::= "[" [Expr2] "]" ;
separator          Expr2      "," ;

EVar.              Expr8  ::= Ident ;
EIndex.            Expr8  ::= Expr8 "[" Expr "]" ;
EElem.             Expr8  ::= Expr8 "." Integer ;

--ECall.             Expr8  ::= Expr8 "(" [Expr8] ")";
--separator          Expr8      "," ;

EMul.              Expr7  ::= Expr7 "*" Expr8 ;
EDiv.              Expr7  ::= Expr7 "/" Expr8 ;
EMod.              Expr7  ::= Expr7 "%" Expr8 ;
EAdd.              Expr6  ::= Expr6 "+" Expr7 ;
ESub.              Expr6  ::= Expr6 "-" Expr7 ;
ENeg.              Expr6  ::= "-" Expr7;

ECmp.              Expr5  ::= Cmp ;
Cmp1.              Cmp    ::= Expr6 CmpOp Expr6 ;
Cmp2.              Cmp    ::= Expr6 CmpOp Cmp ;
CmpEQ.             CmpOp  ::= "==" ;
CmpNE.             CmpOp  ::= "<>" ;
CmpLT.             CmpOp  ::= "<" ;
CmpLE.             CmpOp  ::= "<=" ;
CmpGT.             CmpOp  ::= ">" ;
CmpGE.             CmpOp  ::= ">=" ;

ENot.              Expr5  ::= "not" Expr5 ;
EAnd.              Expr4  ::= Expr5 "and" Expr4 ;
EOr.               Expr3  ::= Expr4 "or" Expr3 ;

ETuple.            Expr1  ::= [Expr3] ;
separator nonempty Expr3      "," ;

coercions          Expr       8 ;


-- types --

internal TDeref.   Type4  ::= Type4 ;
internal TVoid.    Type4  ::= "Void" ;
TInt.              Type4  ::= "Int" ;
TBool.             Type4  ::= "Bool" ;
TObject.           Type4  ::= "Object" ;
TString.           Type4  ::= "String" ;
TArray.            Type4  ::= "[" Type "]" ;
--TPower.            Type3  ::= Type4 "^" Integer ;
TTuple.            Type2  ::= [Type3] ;
separator nonempty Type3      "*" ;
--TFunc.             Type1  ::= Type2 "->" Type1 ;

coercions          Type       4 ;


-- comments --

comment            "#" ;
comment            "/*" "*/" ;